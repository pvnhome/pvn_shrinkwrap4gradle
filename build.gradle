plugins {
    id 'java-library'
    id 'ivy-publish'
    id 'maven-publish'
    id 'eclipse'
}

group = 'kz.pvnhome.pvnsw4gradle'

version = '1.2.0'

assert hasProperty('IVY_REMOTE_REPOSITORY'): "Set the property 'IVY_REMOTE_REPOSITORY' in your your gradle.properties pointing to remote Ivy repository"

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(12)
   }

   // Using the maven-publish or ivy-publish plugin, this will not only automatically create and publish a -sources.jar
   // but also publish the information that these exist as variants in Gradle Module Metadata.
   // If activated, a Java and Java Library project automatically provides the javadocJar and sourcesJar tasks.
   // (since 6.0.1)
   withSourcesJar()
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
   implementation group: 'org.gradle', name: 'gradle-tooling-api', version: '7.4.1'
   implementation group: 'org.jboss.shrinkwrap', name: 'shrinkwrap-api', version: '1.2.6'
   implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

   testImplementation group: 'junit', name: 'junit', version: '4.12'

   testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
   testRuntimeOnly group: 'org.jboss.shrinkwrap', name: 'shrinkwrap-impl-base', version: '1.2.6'
}

jar {
   manifest {
      attributes('Implementation-Title': project.name,
                 'Implementation-Version': project.version)
   }
}

publishing {
   publications {
      ivy(IvyPublication) {
         from components.java
      }
   }
   repositories {
      ivy {
         url IVY_REPOSITORY
         if (IVY_REPOSITORY.startsWith('sftp')) {
            // Credentials for use with SFTP repo only
            credentials {
               username IVY_REPOSITORY_USER
               password IVY_REPOSITORY_PWD
            }
         }
      }
      ivy {
         url IVY_REMOTE_REPOSITORY
         credentials {
            username IVY_REMOTE_REPOSITORY_USER
            password IVY_REMOTE_REPOSITORY_PWD
         }
      }
   }
}